plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
}

group 'ru.ok.byteweaver'
version '1.0.1'

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'ru.ok.byteweaver'
            implementationClass = 'ru.ok.byteweaver.plugin.ByteweaverPlugin'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation gradleApi()

    implementation 'com.android.tools.build:gradle:8.2.2'
    implementation 'org.ow2.asm:asm:9.7'
    implementation 'org.ow2.asm:asm-util:9.7'
    implementation 'org.ow2.asm:asm-commons:9.7'

    testImplementation 'junit:junit:4.13.2'
}

sourceSets.test {
    java.srcDirs += 'src/sample/java'
    resources.srcDirs += 'src/sample/resources'
}

publishing {
    repositories {
        def propUrl = System.getenv("MAVEN_PUBLISH_REPO_URL")
        def propUsername = System.getenv("MAVEN_PUBLISH_USERNAME")
        def propPassword = System.getenv("MAVEN_PUBLISH_PASSWORD")
        if (propUrl != null) {
            maven {
                url = uri(propUrl)
                credentials {
                    username = propUsername
                    password = propPassword
                }
            }
        }
    }
}
